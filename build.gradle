/*
 * Copyright (C) 2018. OpenLattice, Inc.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 *
 * You can contact the owner of the copyright at support@openlattice.com
 *
 */

plugins {
    id 'application'
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'jacoco'
    id 'checkstyle'
    id 'maven-publish'
    id 'signing'
    id 'com.github.spotbugs' version '3.0.0'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.3.61'
}

description = "Bridge"
group = "com.openlattice"
ext.projectName = "bridge"
ext.scmUrl = 'scm:git:https://github.com/openlattice/bridge.git'
ext.connectionUrl = 'scm:git:https://github.com/openlattice/bridge.git'
ext.developerConnectionUrl = 'scm:git:https://github.com/openlattice/bridge.git'

distTar {
    archiveName "bridge.tgz"
}

apply from: "https://raw.githubusercontent.com/openlattice/gradles/master/openlattice.gradle"

mainClassName = "com.openlattice.bridge.Bridge"

def B_XMS = "$System.env.BRIDGE_XMS"
def B_XMX = "$System.env.BRIDGE_XMX"
def B_ARGS = "$System.env.BRIDGE_ARGS"
def PARALLEL = "$System.env.PARALLELISM"
def GC = "$System.env.GC"

if (B_XMS == 'null' || B_XMS == null || B_XMS == "") {
    B_XMS = '-Xms1g'
}

if (B_XMX == 'null' || B_XMX == null || B_XMX == "") {
    B_XMX = '-Xmx4g'
}

if (B_ARGS == 'null' || B_ARGS == null || B_ARGS == "") {
    B_ARGS = 'local postgres medialocal auditlocal'
}


if (PARALLEL == 'null' || PARALLEL == null || PARALLEL == "") {
    PARALLEL = "-Djava.util.concurrent.ForkJoinPool.common.parallelism=" + Runtime.runtime.availableProcessors()
}

if (GC == 'null' || GC == null || GC == "") {
    GC = "-XX:+UseG1GC"
}

println "Using java args for running ${projectName}: Xms=" + B_XMS + ",Xmx=" + B_XMX + " -server"

applicationDefaultJvmArgs = [B_XMS, B_XMX, "-server", GC, PARALLEL]
applicationDefaultJvmArgs += [ "--add-modules", "java.se",
                               "--add-exports","java.base/jdk.internal.ref=ALL-UNNAMED",
                               "--add-opens","java.base/java.lang=ALL-UNNAMED",
                               "--add-opens","java.base/java.nio=ALL-UNNAMED",
                               "--add-opens","java.base/sun.nio.ch=ALL-UNNAMED",
                               "--add-opens","java.management/sun.management=ALL-UNNAMED",
                               "--add-opens","jdk.management/com.sun.management.internal=ALL-UNNAMED"]

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

checkstyle {
    toolVersion = '7.4'
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

spotbugs {
    reportLevel = 'high'
    toolVersion = "${spotbugs_annotations_version}"
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

run {
    if (B_ARGS != null) {
        args B_ARGS.split();
    } else if (System.getProperty("exec.args") != null) {
        args System.getProperty("exec.args").split()
    }
}

println "Enabled profiles: " + Arrays.toString(run.args)

configurations {
    tests
    published.extendsFrom tests, archives

    compile {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
}

ext.odata_version = '4.2.0'
dependencies {
    spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:${findsecbugs_version}"
    compileOnly "net.jcip:jcip-annotations:${jcip_version}"
    compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugs_annotations_version}"
    testCompileOnly "net.jcip:jcip-annotations:${jcip_version}"
    testCompileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugs_annotations_version}"

    if (project.hasProperty('developmentMode') && project.developmentMode) {
        logger.quiet(project.name + " using project dependencies.")
        compile project(":rhizome")
        compile project(":conductor-client")
        compile project(":api")
        testCompile project(":conductor")
        testCompile(project(path: ":api", configuration: "tests"))
        testCompile(project(path: ":rhizome", configuration: "tests"))
        testCompile(project(path: ":rhizome-client", configuration: "tests"))
    } else {
        logger.quiet(project.name + " using jar dependencies.")
        compile "com.openlattice:rhizome:${rhizome_version}"
        compile "com.openlattice:conductor-client:${conductor_client_version}"
        compile "com.openlattice:api:${api_version}"
        testCompile "com.openlattice:conductor:${conductor_version}"
        testCompile "com.openlattice:api:${api_version}:tests"
        testCompile "com.openlattice:rhizome:${rhizome_version}:tests"
        testCompile "com.openlattice:rhizome-client:${rhizome_client_version}:tests"
    }

    compile group: "com.twilio.sdk", name: "twilio", version: "${twilio_version}"

    compile "javax.annotation:javax.annotation-api:$javax_annotation_api_version"
    compile "org.apache.olingo:odata-server-api:${odata_version}"
    compile "org.apache.olingo:odata-server-core:${odata_version}"
    compile "org.apache.olingo:odata-commons-api:${odata_version}"
    compile "org.apache.olingo:odata-commons-core:${odata_version}"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    /*
     * TESTING
     */
    testCompile "junit:junit:${junit_version}"

    testCompile("org.mockito:mockito-all:${mockito_version}") {
        exclude module: 'hamcrest'
        exclude module: 'hamcrest-core'
    }
}

eclipse {
    ext.downloadSources = true
    ext.downloadJavadoc = true

    ext.sourceCompatibility = JavaVersion.VERSION_11
    ext.targetCompatibility = JavaVersion.VERSION_11
}


tasks.withType(JavaCompile) {
    options.compilerArgs+= [ "--add-modules", "java.se",
                             "--add-exports","java.base/jdk.internal.ref=ALL-UNNAMED",
                             "--add-opens","java.base/java.lang=ALL-UNNAMED",
                             "--add-opens","java.base/java.nio=ALL-UNNAMED",
                             "--add-opens","java.base/sun.nio.ch=ALL-UNNAMED",
                             "--add-opens","java.management/sun.management=ALL-UNNAMED",
                             "--add-opens","jdk.management/com.sun.management.internal=ALL-UNNAMED"]
}

tasks.withType(Test) {
    jvmArgs += [ "--add-modules", "java.se",
                 "--add-exports","java.base/jdk.internal.ref=ALL-UNNAMED",
                 "--add-opens","java.base/java.lang=ALL-UNNAMED",
                 "--add-opens","java.base/java.nio=ALL-UNNAMED",
                 "--add-opens","java.base/sun.nio.ch=ALL-UNNAMED",
                 "--add-opens","java.management/sun.management=ALL-UNNAMED",
                 "--add-opens","jdk.management/com.sun.management.internal=ALL-UNNAMED"]
}


tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += '-Xjvm-default=compatibility'
    }
}


jar {
    doFirst {
        manifest {
            attributes (
                    "Class-Path": configurations.runtime.files.collect { it.getName() }.join(" "),
                    "Main-Class": mainClassName
            )
        }
    }
}

startScripts {
    classpath = files(jar.archivePath)
}
